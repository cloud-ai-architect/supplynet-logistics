terraform {
  required_version = ">= 1.9.0"
  required_providers {
    aws     = { source = "hashicorp/aws", version = "~> 5.50" }
    archive = { source = "hashicorp/archive", version = "~> 2.4" }
    random  = { source = "hashicorp/random", version = "~> 3.6" }
  }
}

variable "project_name" { type = string }
variable "environment" { type = string }
variable "name_prefix" { type = string }
variable "lambdas" { type = map(string) }
variable "lambda_runtime" {
  type    = string
  default = "python3.12"
}
variable "lambda_memory_mb" {
  type    = number
  default = 512
}
variable "lambda_timeout" {
  type    = number
  default = 300
}
variable "buckets" { type = map(string) }
variable "tables" { type = map(string) }
variable "vector_index_name" { type = string }
variable "bedrock_model_id" { type = string }
variable "haiku_model_id" { type = string }
variable "lambda_role_arns" { type = map(string) }
variable "api_role_arns" { type = map(string) }
variable "log_retention_days" {
  type    = number
  default = 30
}
variable "extra_env" {
  type    = map(string)
  default = {}
}

variable "common_tags" {
  type    = map(string)
  default = {}
}

locals {
  common_env = {
    ENVIRONMENT         = var.environment
    PROJECT_NAME        = var.project_name
    CATALOG_BUCKET      = var.buckets.catalog
    VECTORS_BUCKET      = var.buckets.vectors
    VECTOR_INDEX        = var.vector_index_name
    ORDERS_TABLE        = var.tables.orders
    FEEDBACK_TABLE      = var.tables.feedback
    CONVERSATIONS_TABLE = var.tables.coversations
    BEDROCK_MODEL_ID    = var.bedrock_model_id
    HAIKU_MODEL_ID      = var.haiku_model_id
    LOG_LEVEL           = "INFO"
  }

  stage_lambdas = {
    orchestrator = "src.lambdas.orchestrator_handler.handler"
    ingest       = "src.lambdas.ingest_handler.handler"
    disruption   = "src.lambdas.disruption_handler.handler"
    reroute      = "src.lambdas.reroute_handler.handler"
    notify       = "src.lambdas.notify_handler.handler"
    feedback     = "src.lambdas.feedback_handler.handler"
  }
}

data "archive_file" "placeholder" {
  type        = "zip"
  output_path = "${path.module}/build/placeholder.zip"
  source {
    content  = <<EOF
import json
def handler(event, context):
    return {"statusCode": 200, "body": json.dumps({"message": "placeholder"})}
EOF
    filename = "handler.py"
  }
}

resource "aws_lambda_function" "this" {
  for_each = var.lambdas

  function_name = each.value
  role          = var.lambda_role_arns[each.key]
  handler       = lookup(local.stage_lambdas, each.key, "src.lambdas.orchestrator_handler.handler")
  runtime       = var.lambda_runtime
  memory_size   = var.lambda_memory_mb
  timeout       = var.lambda_timeout

  filename         = data.archive_file.placeholder.output_path
  source_code_hash = data.archive_file.placeholder.output_base64sha256

  environment {
    variables = merge(local.common_env, var.extra_env)
  }

  tags = var.common_tags

  tracing_config {
    mode = "Active"
  }

  # Terraform provisions the function; application code is delivered by
  # scripts/package_lambdas.py via update-function-code. Without this, every
  # apply reverts the live code to the 248-byte placeholder stub -- which is
  # exactly what happened when the API routes were rewired.
  lifecycle {
    ignore_changes = [filename, source_code_hash]
  }
}

resource "aws_cloudwatch_log_group" "this" {
  for_each          = var.lambdas
  name              = "/aws/lambda/${each.value}"
  retention_in_days = var.log_retention_days
  tags              = var.common_tags
}

output "function_arns" { value = { for k, fn in aws_lambda_function.this : k => fn.arn } }
output "function_names" { value = { for k, fn in aws_lambda_function.this : k => fn.function_name } }
